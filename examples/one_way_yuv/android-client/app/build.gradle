apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "edu.cmu.cs.roundtrip"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        debug {
            buildConfigField("String", "GABRIEL_HOST", properties.getProperty('gabrielHost'))
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.8.0'
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    java {
                        option 'lite'
                    }
                    python {
                        // This gradle file is located in
                        // <REPOSITORY ROOT>/examples/one_way_yuv/android-client/app
                        // outputSubDir is relative to
                        // <REPOSITORY ROOT>/examples/one_way_yuv/android-client/app/build/generated/source/proto/debug
                        outputSubDir = '../../../../../../..'
                    }
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'edu.cmu.cs.gabriel:client:2.2.3'
    implementation 'edu.cmu.cs.gabriel:protocol:2.0.1'
    implementation 'edu.cmu.cs.gabriel:camera:2.2.3'

    def camerax_version = "1.0.0-beta11"
    // CameraX core library using camera2 implementation
    implementation "androidx.camera:camera-camera2:$camerax_version"
    // CameraX Lifecycle Library
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    // CameraX View class
    implementation "androidx.camera:camera-view:1.0.0-alpha18"

    implementation 'com.google.android.material:material:1.3.0-alpha03'
}